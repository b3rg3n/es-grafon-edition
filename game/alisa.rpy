init:
    $ mods["Official_Alice_good_end"] = u"Новая концовка Алисы"
    #$ mod_tags["Official_Alice_good_end"] = ["gameplay:kinetic","length:day","protagonist:male","character:Семён","character:Алиса"]

    
    image dv smile coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_4_body.png",(0,0), "dv/dv_4_coat.png",(0,0), "images/sprites/normal/dv/dv_4_smile.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv normal coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_4_body.png",(0,0), "dv/dv_4_coat.png",(0,0), "images/sprites/normal/dv/dv_4_normal.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv surprise coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_1_body.png",(0,0), "dv/dv_1_coat.png",(0,0), "images/sprites/normal/dv/dv_1_surprise.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv shy coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_3_body.png",(0,0), "dv/dv_3_coat.png",(0,0), "images/sprites/normal/dv/dv_3_shy.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv angry coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_5_body.png",(0,0), "dv/dv_5_coat.png",(0,0), "images/sprites/normal/dv/dv_5_angry.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv cry coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_1_body.png",(0,0), "dv/dv_1_coat.png",(0,0), "images/sprites/normal/dv/dv_1_cry.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv laugh coat = im.MatrixColor( im.Composite((900,1080), (0,0), "images/sprites/normal/dv/dv_4_body.png",(0,0), "dv/dv_4_coat.png",(0,0), "images/sprites/normal/dv/dv_4_laugh.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv laugh coat close = im.MatrixColor( im.Composite((1050,1080), (0,0), "images/sprites/close/dv/dv_4_body.png",(0,0), "dv/dv_4_coat_close.png",(0,0), "images/sprites/close/dv/dv_4_laugh.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv smile coat close = im.MatrixColor( im.Composite((1050,1080), (0,0), "images/sprites/close/dv/dv_4_body.png",(0,0), "dv/dv_4_coat_close.png",(0,0), "images/sprites/close/dv/dv_4_smile.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    image dv surprise coat close = im.MatrixColor( im.Composite((1050,1080), (0,0), "images/sprites/close/dv/dv_1_body.png",(0,0), "dv/dv_1_coat_close.png",(0,0), "images/sprites/close/dv/dv_1_surprise.png"), im.matrix.tint(0.83, 0.88, 0.92) )
    
    image cg epilogue_dv_good_new = "dv/epilogue_dv_good.jpg"

    image dv_kiss:
        contains:
            "bg bus_stop"
            linear 2.0 zoom 1.2
            linear 2.0 zoom 1.0
            
        contains:
            "dv surprise coat close"
            xalign 0.5
            xanchor 0.5
            yanchor 0.0
            linear 2.0 zoom 1.2
            linear 2.0 zoom 1.0
            
label Official_Alice_good_end:
    
    scene black
    
    if not persistent.endings["dv_good"]:
        "Сначала надо получить стандартную хорошую концовку Алисы."
        return
    
    $ backdrop = "epilogue"
    
    $ new_chapter(-1, u"Эпилог")
    
    $ prolog_time()
    
    scene black
    
    $ renpy.pause(2)
    
    window show
    "В голове играла назойливая песенка."
    "Или скорее – просто мелодия, несколько повторяющихся нот."
    "Похоже, электрогитара, причем явно не последняя модель с множеством примочек."
    "Однако звук словно обволакивал меня, уносил куда-то далеко, если это вообще возможно в бесконечной пустоте, в которой, казалось, я нахожусь."
    "Но вот он слабеет, удаляется и вскоре совсем затихает…"
    window hide
    
    scene bg semen_room
    show prologue_dream
    with fade3
    
    play music music_list["drown"] fadein 3
    
    window show
    "Я спросонья приоткрыл один глаз."
    me "Это... был сон?"
    "Просто страшный сон.{w} Или нечто большее?"
    "Да и голова трещит так, как будто вчера выиграл в соревновании «перепей свою совесть»!"
    "И когда это {i}вчера{/i} было?.."
    "Ведь водку мы с Алисой пили позавчера, а потом была долгая дорога, костёр, автобус…"
    window hide
    
    scene bg semen_room
    with fade2
    
    window show
    "Я отвернулся от стены…{w} и оказался в своей квартире!"
    th"Нет, чего-то такого стоило ожидать."
    th"Да, чёрт возьми, почему я должен удивляться?!"
    th"Что, если бы я никогда не выбрался из {i}того{/i} лагеря, остался бы в {i}том{/i} мире навсегда?"
    th"Мог бы считать, что всё нормально, со мной всё в порядке, со всеми всё в порядке и всё со всеми ясно?!" 
    th"Так чего удивляться сейчас?.."
    window hide
    
    with fade2
    
    window show
    "Пошатываясь, я добрался до кухни, достал из холодильника бутылку дешёвого пива, залпом выпил половину.{w} И с отвращением посмотрел на неё, тут же поставив обратно."
    "Спросонья в голове тут же застучало, мир на секунду поплыл куда-то по своим делам, но настроение несколько улучшилось."
    "Или просто стало наплевать."
    window hide
    
    stop music fadeout 5
    
    with fade2
    
    play sound_loop sfx_computer_noise fadein 3
    
    window show
    "В комнате привычно шумел пыльными кулерами компьютер, изредка потрескивали колонки, а за окном валил снег, ещё отчётливее напоминая, что хоть вчера и было лето, но сейчас, очевидно, зима."
    "Ну и славненько, ну и хорошо – кошмар закончился, через час я уже и забуду об этом дурацком сне!"
    "В конце концов, так ведь устроена человеческая психика."
    "Однако интернет-форумы, мессенджеры и даже музыка, игравшая из дешёвеньких колонок системы «качаю только lossless», спасали слабо."
    "В памяти то и дело вспыхивали картины из «Совёнка», они казались такими далекими, но в то же время родными, как выцветшая фотография на Polaroid, родом из детства."
    "Если подумать, то прошло всего ничего – сутки, меньше?{w} Но было ли всё это на самом деле?"
    "А если было, то ушло ли оно навсегда?.."
    
    play sound_loop2 sfx_keyboard_mouse fadein 2
    
    "Мне стало страшно признаваться самому себе, что я не хочу вот так вот пялиться в опостылевший монитор, не хочу слышать омерзительное клацанье мышки и треск клавиатуры, набивающей нестройные ряды бессмысленных букв."
    "А хочу я – назад, в лето, в лагерь, к девочкам, даже к Электронику, чтоб его!{w} И к Алисе…"
    "По щекам неожиданно потекли горячие соленые слёзы."
    "Так бывает: ты не хочешь плакать, потому что не понимаешь, зачем оно надо."
    "Но кто бы тебя ещё спрашивал!"
    "Не всё в жизни можно оценить логически, не все ответы – найти."
    "Вот и сейчас я готов отдать всё, что у меня есть, и всё, что когда-либо будет, за возможность вернуться обратно."
    "Пусть даже мне не известно будущее, наплевать! На месте разберёмся."
    "Может быть, тогда, неделю назад, кто-то услышал моё подсознательное желание, в котором я боялся признаться даже самому себе, и перенёс в «Совёнок»?{w} Чтобы потом вот так вот вернуть назад?.."
    "Но ведь это несправедливо!"
    "Хотя я четко понимал, что, дай мне выбор, я бы не стал ничего менять!{w} Потому что лучше быть счастливым на миг, чем вообще не познать этого счастья."
    "Хотя, может быть, не всё ещё потеряно?.."
    
    stop sound_loop fadeout 2
    stop sound_loop2 fadeout 2
    
    "…"
    window hide
    
    scene black
    with fade3
    
    play music music_list["farewell_to_the_past_full"] fadein 3
    
    $ renpy.pause(3)
    
    $ set_mode_nvl()
    
    window show
    nvl clear
    "Время шло, жизнь постепенно возвращалась в привычное русло – какими бы сильными ни были {i}те{/i} переживания, но всё имеет свойство истираться из памяти, словно песчинки, уносимые вдаль бурей."
    "К тому же что-то в моей жизни определённо изменилось, я стал более открытым, легче общался со знакомыми и находил контакт с незнакомыми людьми."
    "Многие заметили изменения, произошедшие во мне, да просто не могли не заметить!"
    "Вскоре из пыльного угла я достал гитару."
    "Первые аккорды давались с трудом, но каждый раз, когда так мучительно хотелось всё бросить, я вспоминал Алису."
    "Она стояла перед глазами и укоризненно смотрела на меня."
    "А когда всё получалось – улыбалась и смеялась, как в ту лунную ночь на сцене."
    nvl clear
    "Сложно научиться чему-то по самоучителю в своём воображении, но в качестве мотиватора воображаемая Алиса подходила идеально."
    "Мне было страшно опозориться перед ней, и совсем не потому что я боялся её гнева."
    "Просто вспоминал, как она играла {i}тогда{/i} – на гитаре, больше напоминающей лодочное весло, через паршивые колонки, без усилителя, не имея под рукой интернета, чтобы посмотреть табулатуру, или возможности заниматься с репетитором."
    "Играла искренне, вся отдаваясь музыке!"
    "Может быть, и у меня когда-нибудь получится так же, но для этого придётся постараться! Пожалуй, впервые в жизни я был по-настоящему готов приложить максимум усилий, несмотря ни на что, не страшась трудностей и неудач, не останавливаясь, пока не добьюсь своего!"
    "В итоге у меня начало что-то получаться, да просто должно было – столько времени я занимался! Но хотелось не просто играть чужие песни, но и сочинять свои."
    nvl clear
    "Это оказалось ничуть не проще, а во многом и труднее, чем освоить гитару – ведь тут приходилось думать уже не только о себе, но и быть фактически всей группой: ритм-секция должна звучать в унисон, вот здесь надо добавить клавиш, тут – струнные, здесь выходит ритм-гитара, а тут – соло…"
    "У меня не было каких-то чётких предпочтений – песни получались в разных жанрах, от попсы до метала."
    "Однако становиться человеком-оркестром в мои планы не входило, поэтому следующим шагом стало создание группы."
    "Сначала, как ни странно, нашёлся барабанщик, потом – басист, потом – клавишник."
    "Оставалось дело за вторым гитаристом, ведь я не был уверен, что осилю все партии в одиночку."
    "Да и с двумя гитарами музыкальный диапазон шире."
    "У нас было несколько склёпанных из сэмплов песен, и мне казалось, что ещё немного – и начнём репетировать…"
    "Но кому-то не нравился стиль нашей музыки, кому-то – неопытность участников, кому-то – даже название, поэтому поиски второго гитариста затянулись." 
    nvl clear
    "В итоге мы решили всё-таки начать репетировать."
    "Но что-то пошло не так с самого начала. Подобное случается: у каждого своё видение музыки, разный уровень игры, да в конце концов, можно просто не сойтись характерами."
    "И моя первая группа развалилась после нескольких репетиций."
    "Конечно, я был расстроен, но не опустил руки и вскоре смог найти новых участников."
    "С тем же результатом..."
    
    stop music fadeout 5
    
    "..."
    nvl clear
    window hide
    
    pause(2)
    
    scene bg semen_room_window
    with fade3
    
    play music music_list["a_promise_from_distant_days_v2"] fadein 3
    
    window show
    "Зима подошла к концу, и началась весна."
    "Однако, как это часто бывает в наших краях, смена времён года отразилась только на календаре – а за окном по-прежнему трещали морозы, и бушевала метель." #Предзнаменование метели на встрече
    "Именно такая погода и сегодня, мне даже на секунду показалось, что и не было последних месяцев и моих занятий на гитаре, не было никаких неудач с группой, и вообще я только что очнулся после возвращения из «Совёнка»."
    "Ну или после сна о том лагере – сейчас уже наверняка сказать нельзя."
    "Однако воспоминания оставили после себя не скорбную тоску, а радость и светлую грусть."
    "Мои размышления прервало мигающее окно сообщений."
    nvl clear
    window hide
    
    scene bg semen_room
    with fade2
    
    window show
    message "Привет, вам нужен второй гитарист?"
    me "Да, нужен, привет."
    "Быстро ответил я."
    "В конце концов, не так уж и часто мне кто-то пишет, а уж по делу…"
    message "Ну вот я и пишу."
    me "Хорошо, спасибо."
    me "Давно играешь?"
    message "Вообще, играю с детства, но раньше – на акустике."
    message "А так вот чтобы серьезно заниматься начала – недавно."
    me "Ясно."
    me "Ну, я тоже начинающий, ты понимаешь."
    message "Понимаю, это ничего."
    nvl clear
    "Мы обменялись ещё парой десятков ничего не значащих сообщений и договорились встретиться."
    "Конечно, логично было бы сразу назначить встречу на репетиционной базе, чтобы оценить уровень мастерства друг друга, но ведь группа, коллектив – это не только сумма профессиональных навыков, но и общность людей, характеров."
    "Многие известные группы разваливались совсем не потому, что кто-то не попадал по струнам…"
    
    stop music fadeout 5
    
    "..."
    nvl clear
    window hide
    
    pause(2)
    
    $ set_mode_adv()
    
    scene bg bus_stop
    show snow
    with fade2
    
    play ambience ambience_cold_wind_loop fadein 3
    
    window show
    "И вновь я на остановке 410-го автобуса."
    "Интересно, увезёт ли он меня сегодня в «Совёнок»?{w} И главное – хочу ли я сам?"
    "Ведь сейчас у меня есть дела, которые ждать не будут – да хоть встреча с этой гитаристкой."
    "Какой-никакой, а прогресс – многие отказывали сразу, до личного знакомства дело не доходило."
    "Снег шёл всё сильнее, я поднял воротник и застегнул пальто."
    "Ну когда уже наконец лето?.."
    "Тёплое, как тогда – в «Совёнке»."
    "На секунду взгрустнулось, но я тут же отогнал эти мысли – негоже при первом знакомстве с девушкой выглядеть рохлей."
    "Хотя мы же собираемся просто играть вместе, а не…"
    "Тоже мне – «просто»!"
    "Интересно, а какая она?{w} Понятно, что внешность обычно не коррелирует с профессиональными качествами, но всегда приятнее на сцене стоять рядом с симпатичной девушкой, а не со 150-килограммовым бегемотом…"
    "И почему мы вообще договорились встретиться именно на этой остановке – больно резанула по сознанию мысль, которая должна была прийти в голову гораздо раньше."
    dvg "Привет, меня ждешь?"
    
    play sound sfx_wind_gust
    
    "Я обернулся, но мгновенно налетевший порыв ветра чуть не сбил меня с ног, а снег попал в глаза и в рот."
    "Девушка громко – даже чересчур – засмеялась."
    me "Извини."
    "Отплевываясь, промямлил я."
    
    stop ambience fadeout 2
    
    dvg "Надеюсь, ты не такой же неуклюжий на сцене?"
    
    play music music_list["everlasting_summer"] fadein 5
    
    show dv smile coat at center with dissolve
    "Наконец я протёр глаза и посмотрел на неё."
    show dv normal coat at center with dspr
    "Передо мной стояла Алиса!{w} Нет, девушка, очень похожая на неё!"
    "Кажется, удивилась и она, но выглядела спокойнее."
    dvg "А мы раньше нигде не встречались?"
    me "Вот и мне кажется…{w} То есть нет! Конечно встречались! В лагере, в «Совёнке», помнишь?"
    "Она пристально посмотрела на меня, как будто силясь что-то вспомнить."
    show dv surprise coat at center with dspr
    dvg "В лагере?.."
    dvg "Нет, не помню, то есть не знаю, в каком лагере? Я никогда не была ни в каких лагерях."
    me "Не верю."
    "Сказал я тихо."
    "Да я просто не хотел верить – я знаю, что передо мной именно Алиса, и плевать на всю мистику!"
    "Я просто чувствую, знаю!"
    show dv shy coat at center with dspr
    dvg "Я… я… не знаю."
    "Девушка смутилась и отвела взгляд."
    show dv angry coat at center with dspr
    dvg "Не знаю я, и всё тут!"
    "Топнула она ногой и сверкнула глазами."
    dvg "Может быть, но всё это было во сне, не по-настоящему."
    me "Во сне? То есть ты считаешь, что два человека могут видеть один и тот же сон?"
    dvg "Откуда я знаю, что там видел ты."
    "Девушка отвернулась."
    me "А я думаю, что знаешь! Лагерь, вечер с гитарой на сцене, бомбоубежище под старым корпусом, водка, наконец, ну и…"
    "Я смутился."
    me "Ну ты понимаешь…"
    show dv cry coat at center with dspr
    dvg "В том-то и дело, что не понимаю."
    "Алиса – теперь я был точно уверен – посмотрела на меня заплаканными глазами."
    dv "Я хотела бы понять, но не могу!{w} В первые секунды после {i}возвращения{/i} всё казалось таким реальным…{w} А сейчас я уже и не знаю."
    dv "Если ты и правда Семён… Тогда я думала, что мы уедем{w} вместе."
    dv "Окажемся в этом райцентре или ещё где – неважно!{w} А потом…"
    "Она всхлипнула и продолжила:"
    dv "Да мне вообще казалось, что это всего лишь сон!"
    dv "Я проснулась и не могла понять, что настоящее? Здесь или там?{w} И кто – я или {i}она{/i}…"
    dv "Но в итоге мне стало всё равно – {i}та{/i} я просто хотела остаться там."
    "Она остановилась на секунду."
    dv "… с тобой."
    window hide
    
    scene dv_kiss
    show snow
    with dissolve
    
    window show
    "Я больше не мог терпеть и сжал Алису в своих объятиях."
    "Ведь казалось, что всё осталось там – во сне, – и нам не суждено встретиться наяву."
    "Я и сам в это поверил."
    "Но у моей сказки, похоже, всё же будет счастливый конец!"
    me "Прости…"
    "Тихо прошептал я."
    
    scene bg bus_stop
    show snow
    
    show dv laugh coat close with dspr
    dv "За что ты постоянно извиняешься?"
    "Алиса с трудом рассмеялась."
    me "Не знаю, просто… Просто хочется извиниться."
    me "Потому что, наверное, я в чём-то виноват."
    show dv smile coat close with dspr
    dv "Да, ты определённо виноват!"
    me "В чём?"
    "Удивлённо спросил я."
    dv "Не знаю."
    window hide
    
    scene cg epilogue_dv_good_new
    show snow
    with fade3
    
    pause(2)
    window show
    "Ответила Алиса и впилась мне в губы жарким поцелуем."
    "Как тогда, ночью у костра, но только сейчас это по-настоящему."
    "Словно и не было недели, проведённой в «Совёнке»!"
    "Может быть, то – всего лишь вещий сон, предваряющий нашу неизбежную встречу наяву."
    "Я не верю в судьбу, но ещё меньше мне хочется верить в злой рок."
    "А ведь ещё пару минут назад все события, произошедшие со мной за последнее время, казались как раз его злой волей!"
    "Так что пусть всё идет так, как и должно!"
    me "Знаешь, а я верю!"
    "Оторвался я на миг от Алисы."
    dv "Во что?"
    me "Что это всё не просто так! Может быть, «Совёнок» – лишь сон, но приснился он нам с какой-то целью."
    window hide
    
    scene bg bus_stop
    show dv shy coat at center
    show snow
    with fade2
    
    window show
    "Алиса вдруг громко рассмеялась, а потом тут же покраснела и отошла от меня на шаг."
    dv "То есть получается, мы с тобой первый раз видимся?.."
    dv "То есть…{w} мы не знакомы?"
    me "Выходит, что так…"
    dv "Тогда…"
    show dv angry coat at center with dspr
    "Она всё больше краснела и, похоже, начала злиться."
    dv "Я не целуюсь с незнакомцами! Я не такая! Да если ты!.."
    "Алиса попыталась влепить мне пощечину, но слишком безвольно – я легко увернулся."
    me "Хоть что-то в этом мире и в прошлом постоянно."
    "Улыбнулся я."
    show dv smile coat at center with dspr
    dv "Ладно."
    "На её лице вновь появилась несколько надменная улыбка."
    dv "Хотя если ты играешь на том же уровне, что и тогда, то мне с вами делать нечего."
    me "У тебя всегда есть шанс проверить!"
    "Я протянул ей руку."
    
    hide snow
    show heavy_snow
    
    "Снег стал ещё сильнее, но ветер стих, на автобусной остановке стояла тишина, и даже окна в соседних домах потухли, словно наблюдая за нами."
    dv "Знаешь, а ты не такой уж и неудачник!"
    "Алиса взяла меня за руку…"
    window hide

    scene black
    with fade3
    
    pause(1)
    
    window show
    "Теперь я точно уверен, что смогу чего-то добиться в музыке."
    "Вместе с Алисой!"
    window hide
    
    stop music fadeout 5
    
    $ renpy.pause(5)
            
    return

